/mob/living/simple_animal/hostile/abnormality/watchman
	name = "The Watchman"
	desc = "A man holding a large lantern. The lantern, despite having a visible flame, gives off no light."
	icon = 'ModularLobotomy/_Lobotomyicons/32x64.dmi'
	icon_state = "watchman"
	icon_living = "watchman"
	portrait = "watchman"
	del_on_death = TRUE
	maxHealth = 1200
	health = 1200
	gender = MALE
	rapid_melee = 2
	move_to_delay = 6
	damage_coeff = list(RED_DAMAGE = 1, WHITE_DAMAGE = 1.2, BLACK_DAMAGE = 0.5, PALE_DAMAGE = 2)
	melee_damage_lower = 16
	melee_damage_upper = 20			//He doesn't really attack but I guess if he does he would deal this kind of damage
	melee_damage_type = BLACK_DAMAGE
	stat_attack = HARD_CRIT
	attack_sound = "swing_hit"
	attack_verb_continuous = "bashes"
	attack_verb_simple = "bash"
	faction = list("neutral", "hostile")
	can_breach = TRUE
	threat_level = HE_LEVEL
	start_qliphoth = 3
	work_chances = list(
		ABNORMALITY_WORK_INSTINCT = 35,
		ABNORMALITY_WORK_INSIGHT = 50,
		ABNORMALITY_WORK_ATTACHMENT = 35,
		ABNORMALITY_WORK_REPRESSION = 10,
	)
	work_damage_amount = 7
	work_damage_type = BLACK_DAMAGE
	chem_type = /datum/reagent/abnormality/sin/pride

	ego_list = list(
		/datum/ego_datum/weapon/alleyway,
		/datum/ego_datum/armor/alleyway,
	)

	gift_type =  /datum/ego_gifts/alleyway
	light_color = "FFFFFFF"
	light_power = -10

	observation_prompt = "\"Natureless creatures roam the night, you should find shelter.\" <br>\
		The watchman beckons you over. <br>You..."
	observation_choices = list(
		"Approach" = list(TRUE, "Good. <br>It's not safe to roam the woods at night.<br>\
			Come now, I will guide you home."),
		"Run away" = list(FALSE, "You don't get far before you start hearing howling and shrieking. <br>\
			Numerous talons, claws, and fangs bite into you all at once. <br>Now you will know why you fear the night."),
	)

	generic_bubbles = alist(
		1 = list("%ABNO stares at %PERSON with souless glowing eyes."),
		2 = list("%PERSON can hear the jingling of keys coming from %ABNO."),
		3 = list("%ABNO looks wearily at %PERSON."),
		4 = list("%ABNO's lantern increases in it's intensity.", "The flickering flames eminating from %ABNO snuff out, for just a moment"),
		5 = list("%ABNO stares into the eyes of %PERSON, studying them intensely.", "%ABNO watches the hands of %PERSON, waiting for them to make a move"),
	)
	work_bubbles = list(
		ABNORMALITY_WORK_INSTINCT = list("%PERSON gives %ABNO a coin for their troubles.",),
		ABNORMALITY_WORK_INSIGHT = list("%PERSON puts on a bit of music for %ABNO. %ABNO smiles."),
		ABNORMALITY_WORK_ATTACHMENT = list("%PERSON mumbles something to %ABNO, who nods.", "%ABNO whispers something, barely audibly to %PERSON."),
		ABNORMALITY_WORK_REPRESSION = list("%ABNO whispers threats to %PERSON.", "%ABNO brandishes it's lantern up high, menacingly."),
	)

	// Speech Lines
	speak_chance = 4
	var/speak_normal = list(
		"#The night is upon us, find somewhere safe.",
		"#Another night, another shift.",
		"#Stay safe out here.",
		"#It's not safe to roam the streets at night.",
		"#Be careful of what may lie in the dark.",
	)
	var/speak_alert = list(
		"Creatures roam the night, you should find shelter.",
		"The night has become home to many creatures of the dark, be careful.",
		"It's not safe out, return home.",
		"This darkness hides evil within it, stay safe.",
		"I pray the beings of the night return to their dens soon...",
	)
	var/speak_attacked_human = list(
		"#Nothing better to do than hit the Watchman..?",
		"#Kids these days...",
		"#The audacity of some people..!",
		"#May your woes be many and your days few.",
	)
	var/speak_attacked_monster = list(
		"Begone, foul creature of the night!",
		"Your kind are unwelcome here!",
		"Never should have come here!",
		"This darkness is not for you and you alone, monster!",
	)

	var/beneficial = TRUE
	//A numerical cycle for when watchman applies buffs to surrounding humans
	var/buff_cycle = 1
	// Breached Abno tracker.
	// Remembers enemies by their tag.
	var/list/dangers = list()

/mob/living/simple_animal/hostile/abnormality/watchman/FailureEffect(mob/living/carbon/human/user, work_type, pe)
	. = ..()
	datum_reference.qliphoth_change(-1)
	return

/mob/living/simple_animal/hostile/abnormality/watchman/NeutralEffect(mob/living/carbon/human/user, work_type, pe)
	. = ..()
	if(prob(30))
		datum_reference.qliphoth_change(-1)
	return

/mob/living/simple_animal/hostile/abnormality/watchman/PostWorkEffect(mob/living/carbon/human/user, work_type, pe, work_time)
	user.hallucination += 20	//You're gonna be hallucinating for a while


/mob/living/simple_animal/hostile/abnormality/watchman/BreachEffect(mob/living/carbon/human/user, breach_type)
	. = ..()
	set_light(30)	//Makes everything around it really dark, That's all it does lol



//Applies buffs if he's breached and you're near him
/mob/living/simple_animal/hostile/abnormality/watchman/Life()
	. = ..()
	buff_cycle++
	if(beneficial && buff_cycle >= 5)
		buff_cycle = 0
		for(var/mob/living/carbon/human/H in view(8, get_turf(src)))
			H.apply_lc_black_strength(3)
			H.adjustSanityLoss(-10)



/mob/living/simple_animal/hostile/abnormality/watchman/bullet_act(obj/projectile/Proj)
	. = ..()
	if(!ishuman(Proj.firer))
		return
	beneficial = FALSE


/// ======================SPEECH CODE======================
/mob/living/simple_animal/hostile/abnormality/watchman/attacked_by(obj/item/I, mob/living/user)
	. = ..()
	if(ishuman(user))
		beneficial = FALSE
	user.apply_lc_fragile(3)
	if(speak_chance)
		if(prob(speak_chance*2))
			say(pick(speak_attacked_human))

/mob/living/simple_animal/hostile/abnormality/watchman/attack_hand(mob/living/carbon/human/M)
	. = ..()
	M.apply_lc_fragile(3)
	if(speak_chance)
		if(prob(speak_chance*2))
			say(pick(speak_attacked_human))

/mob/living/simple_animal/hostile/abnormality/watchman/attack_animal(mob/living/simple_animal/M)
	. = ..()
	if(speak_chance)
		if(prob(speak_chance*2))
			say(pick(speak_attacked_monster))

/mob/living/simple_animal/hostile/abnormality/watchman/proc/HandleSpeech()
	// End cleaning up the list.
	if(speak_chance)
		GaspWhatWasThat()
		if(prob(speak_chance))
			if(length(dangers))
				say(pick(speak_alert))
			else
				say(pick(speak_normal))

/mob/living/simple_animal/hostile/abnormality/watchman/proc/GaspWhatWasThat()
	//Havent seen them in a while
	popleft(dangers)
	// Add new threats.
	for(var/mob/living/simple_animal/hostile/H in view(7, src))
		if(H == src)
			continue
		if(H.stat == DEAD)
			//Welp, rest in piss beast.
			dangers -= H.tag
			continue
		if(istype(H, /mob/living/simple_animal/hostile/abnormality))
			var/mob/living/simple_animal/hostile/abnormality/A = H
			if(A.IsContained())
				continue
		LAZYOR(dangers,H.tag)
	return dangers

/mob/living/simple_animal/hostile/abnormality/watchman/handle_automated_action()
	. = ..()
	HandleSpeech()

/mob/living/simple_animal/hostile/abnormality/watchman/handle_automated_movement()
	. = ..()
	HandleSpeech()

/mob/living/simple_animal/hostile/abnormality/watchman/patrol_step(dest)
	. = ..()
	HandleSpeech()


